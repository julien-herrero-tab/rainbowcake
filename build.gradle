buildscript {
    ext.rainbowcake_version = '1.4.0'

    ext.kotlin_version = '1.5.30'
    ext.coroutines_version = '1.5.1'

    ext.dagger_version = '2.38.1'
    ext.koin_version = '3.1.2'
    ext.hilt_version = '2.37'

    ext.target_sdk = 30
    ext.min_sdk = 19
    ext.compile_sdk = 30

    repositories {
        google()
        mavenCentral()
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:4.2.2'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'org.jetbrains.kotlinx:binary-compatibility-validator:0.3.0'
        classpath "io.github.gradle-nexus:publish-plugin:1.1.0"
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
    }
}

subprojects { project ->
    group = 'co.zsmb'
    version = rainbowcake_version
    afterEvaluate { p ->
        if (p.hasProperty("android")) {
            setAndroidProps(p)
        }
    }
}

void setAndroidProps(Project p) {
    p.android {
        compileSdkVersion compile_sdk

        defaultConfig {
            minSdkVersion min_sdk
            targetSdkVersion target_sdk

            testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        }

        buildTypes {
            release {
                minifyEnabled false
            }
            debug {
                multiDexEnabled true
            }
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

apply plugin: 'binary-compatibility-validator'

apiValidation {
    ignoredProjects += [
            'demo'
    ]
    nonPublicMarkers += [
            'co.zsmb.rainbowcake.internal.InternalRainbowCakeApi'
    ]
}

apply plugin: 'io.github.gradle-nexus.publish-plugin'
apply from: "${rootDir}/scripts/publish-root.gradle"
